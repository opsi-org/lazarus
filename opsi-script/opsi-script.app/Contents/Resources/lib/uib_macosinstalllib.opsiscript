; This code is part of the opsi.org project
;
; Copyright (c) uib gmbh (www.uib.de)
; This sourcecode is owned by the uib gmbh, D55118 Mainz, Germany
; and published under the Terms of the GNU Affero General Public License.
; Text of the AGPL: http://www.gnu.org/licenses/agpl-3.0-standalone.html


encoding=utf8

deffunc install_macos_pkg($mypkg$ : string) : string
	defvar $exitcode$
	defvar $goon$
	
	set $result$ = "false"
	if fileexists($mypkg$)
		set $exitcode$ = shellcall("installer -allowUntrusted -verbose -dumplog -pkg '"+$mypkg$+"' -target /")
		if not($exitcode$ = "0")
			logerror "installer did not end with exitcode 0. - installation failed."
			;includelog "/tmp/installer.log" "50" 
		else
			; installation success
			set $result$ = "true"
		endif
	else
		LogError $mypkg$+" is not a file as expected. - Abort installation."
	endif
endfunc


deffunc install_macos_dmg($mydmg$ : string) : string
	defStringlist $outlist$
	defStringlist $filelist$
	defvar $outline$
	defVar $regexp$
	defVar $mountdevice$
	defVar $mountpoint$
	defvar $exitcode$
	defvar $goon$
	defvar $workdir$
	set $result$ = "false"
	set $goon$ = "true"
	set $workdir$ = "/tmp/opsimountpoint"

	; todo test file, test extension
	if fileexists($mydmg$)
		if not(ExtractFileExtension ($mydmg$) = ".dmg")
			LogError $mydmg$ + " has not the extension .dmg as expected. - Abort installation."
			set $goon$ = "false"
		endif
	else
		LogError $mydmg$ + " is not a file as expected. - Abort installation."
		set $goon$ = "false"
	endif
	if $goon$ = "true"
		; convert to supress license agreement
		; https://superuser.com/questions/221136/bypass-a-licence-agreement-when-mounting-a-dmg-on-the-command-line
		set $filelist$ = shellcall('hdiutil convert -quiet "'+$mydmg$+'" -format UDTO -o /tmp/converted.cdr')
		set $filelist$ = shellcall('hdiutil attach -quiet -nobrowse -noverify -noautoopen -mountpoint '+$workdir$+' /tmp/converted.cdr')
		set $filelist$ = shellcall("ls -1 "+$workdir$)
		; do we have a .app
		set $regexp$ =  "(?i)\.app$"
		set $outlist$ = getSubListByContainingRegex($regexp$,$filelist$)
		if count($outlist$) > "0"
			; use the first one
			set $outline$ = trim(takeString("0" , $outlist$))
			; is it a directory (it should)
			if directoryexists($workdir$+"/"+$outline$)
				; we have found the .app directory inside the dmg and now we copy it
				markerrornumber
				Files_copy_app
				shellCall('chmod ugo+x "/Applications/'+$outline$+'/Contents/Macos/*')
				if errorsOccurredSinceMark = 0
					; installation success
					set $result$ = "true"
				else
					logerror "Copy files from "+$outline$+" to Application Dir failed. - installation failed."
				endif
			else
				LogError $outline$+" inside the dmg is not a directory as expected. - installation failed."
				set $goon$ = "false"
			endif
		else
			set $regexp$ =  "(?i)\.pkg$"
			set $outlist$ = getSubListByContainingRegex($regexp$,$filelist$)
			if count($outlist$) > "0"
				; install pkg
				; https://apple.stackexchange.com/questions/72226/installing-pkg-with-terminal
				; use the first one
				set $outline$ = trim(takeString("0" , $outlist$))
				; is it a file (it should)
				if fileexists($workdir$+"/"+$outline$)
					set $result$ = install_macos_pkg($workdir$+"/"+$outline$)
				else
					LogError $outline$+" is not a file as expected. - Abort installation."
					set $goon$ = "false"
				endif
			else
				LogError $outline$+" does wether contain a .app dir nor a pkg file. - Abort installation."
			endif
		endif
		; unmount
		set $exitcode$ = shellcall("hdiutil detach "+$workdir$)
		if not($exitcode$ = "0")
			logerror "hdiutil did not end with exitcode 0"
		endif
		; shellcall("hdiutil detach "+$mountdevice$)
		Files_cleanup
	endif
	
	[Files_cleanup]
	del -f /tmp/installer.log
	del -f /tmp/converted.cdr
	del -f $workdir$

	[Files_copy_app]
	copy -s "$workdir$/$outline$/*" "/Applications/$outline$/"
endfunc

deffunc install_macos_zip($myzip$ : string) : string
	defStringlist $outlist$
	defvar $outline$
	defVar $regexp$
	defvar $exitcode$
	defvar $goon$
	defvar $workdir$
	set $result$ = "false"
	set $goon$ = "true"
	set $workdir$ = "/tmp/opsimountpoint"

	; todo test file, test extension
	if fileexists($myzip$)
		if not(ExtractFileExtension ($myzip$) = ".zip")
			LogError $myzip$ + " has not the extension .zip as expected. - Abort installation."
			set $goon$ = "false"
		endif
	else
		LogError $myzip$ + " is not a file as expected. - Abort installation."
		set $goon$ = "false"
	endif
	if $goon$ = "true"
		Files_unzip
		if errorsOccurredSinceMark > 0
			logerror "Unzip files from "+$myzip$+" to temporary Dir failed. - installation failed."
			set $goon$ = "false"
		endif
		if $goon$ = "true"
			set $outlist$ = shellcall("ls -1 "+$workdir$)
			; do we have a .app
			set $regexp$ =  "(?i)\.app$"
			set $outlist$ = getSubListByContainingRegex($regexp$,$outlist$)
			if count($outlist$) > "0"
				; use the first one
				set $outline$ = trim(takeString("0" , $outlist$))
				; is it a directory (it should)
				if directoryexists($workdir$+"/"+$outline$)
					; we have found the .app directory inside the dmg and now we copy it
					markerrornumber
					Files_copy_app
					if errorsOccurredSinceMark = 0
						; installation success
						set $result$ = "true"
					else
						logerror "Copy files from "+$outline$+" to Application Dir failed. - installation failed."
					endif
				else
					LogError $outline$+" inside the zip is not a directory as expected. - installation failed."
					set $goon$ = "false"
				endif
			else
				set $regexp$ =  "(?i)\.pkg$"
				set $outlist$ = getSubListByContainingRegex($regexp$,$outlist$)
				if count($outlist$) > "0"
					; install pkg
					; https://apple.stackexchange.com/questions/72226/installing-pkg-with-terminal
					; use the first one
					set $outline$ = trim(takeString("0" , $outlist$))
					; is it a file (it should)
					if fileexists($workdir$+"/"+$outline$)
						set $result$ = install_macos_pkg($workdir$+"/"+$outline$)
					else
						LogError $outline$+" is not a file as expected. - Abort installation."
						set $goon$ = "false"
					endif
				else
					LogError $outline$+" does wether contain a .app dir nor a pkg file. - Abort installation."
				endif
			endif
		endif
		Files_cleanup
	endif
	
	[Files_cleanup]
	del -f /tmp/installer.log
	del -sf $workdir$
	
	[Files_unzip]
	checktargetpath = $workdir$
	unzipfile "$myzip$" $workdir$

	[Files_copy_app]
	copy -s "$workdir$/$outline$/*" "/Applications/$outline$/"
endfunc

